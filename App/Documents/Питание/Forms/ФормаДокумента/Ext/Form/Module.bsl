
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Дата = ТекущаяДата();
	КонецЕсли;
	
	ЭтаФорма.Заголовок = Формат(Объект.Дата,"ДЛФ=DD") + " (" + ОбщиеФункцииНаСервере.ПолучитьДеньНедели(Объект.Дата) + ")";
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ПитаниеПродукты.ПриемПищи КАК ПриемПищи
	               |ИЗ
	               |	Документ.Питание.Продукты КАК ПитаниеПродукты
				   |ГДЕ
				   |	ПитаниеПродукты.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания";
	
	Запрос.УстановитьПараметр("ДатаНачала",НачалоДня(Объект.Дата));
	Запрос.УстановитьПараметр("ДатаОкончания",КонецДня(Объект.Дата));
	
	РЗ = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ТекСтрока Из РЗ Цикл 
		
		ПриемПищи = СокрЛП(СтрЗаменить(ТекСтрока.ПриемПищи," ",""));
		
		МассивТипаВыбора = Новый Массив;
		МассивТипаВыбора.Добавить(Тип("ТаблицаЗначений"));
		ОписаниеТипаВыбора = Новый ОписаниеТипов(МассивТипаВыбора);
		МассивРеквизитов = Новый Массив;
		МассивРеквизитов.Добавить(Новый РеквизитФормы("Таблица"+ПриемПищи, ОписаниеТипаВыбора, "", ПриемПищи));
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПитаниеПродукты.Продукт КАК Продукт"+ПриемПищи+",
		               |	ПитаниеПродукты.Порция КАК Порция"+ПриемПищи+",
					   |	ПитаниеПродукты.Продукт.Белки * ПитаниеПродукты.Порция / ЕСТЬNULL(ПитаниеПродукты.Продукт.ЕдиницаИзмерения.Кратность,1) КАК Белки"+ПриемПищи+",
		               |	ПитаниеПродукты.Продукт.Жиры * ПитаниеПродукты.Порция / ЕСТЬNULL(ПитаниеПродукты.Продукт.ЕдиницаИзмерения.Кратность,1) КАК Жиры"+ПриемПищи+",
		               |	ПитаниеПродукты.Продукт.Углеводы * ПитаниеПродукты.Порция / ЕСТЬNULL(ПитаниеПродукты.Продукт.ЕдиницаИзмерения.Кратность,1) КАК Углеводы"+ПриемПищи+",
		               |	ПитаниеПродукты.Продукт.ПищеваяЦенность * ПитаниеПродукты.Порция / ЕСТЬNULL(ПитаниеПродукты.Продукт.ЕдиницаИзмерения.Кратность,1) КАК ПищеваяЦенность"+ПриемПищи+"
		               |ИЗ
		               |	Документ.Питание.Продукты КАК ПитаниеПродукты
					   |ГДЕ
					   |	ПитаниеПродукты.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
					   |И
					   |	ПитаниеПродукты.ПриемПищи = &ПриемПищи";
		
		Запрос.УстановитьПараметр("ПриемПищи",ТекСтрока.ПриемПищи);
		
		ТЗ = Запрос.Выполнить().Выгрузить();                           
		
		Для Каждого Колонка Из ТЗ.Колонки Цикл
			
			МассивРеквизитов.Добавить(Новый РеквизитФормы(Колонка.Имя,Колонка.ТипЗначения,"Таблица"+ПриемПищи));
			
		КонецЦикла;
		
		ИзменитьРеквизиты(МассивРеквизитов);
		
		ТаблицаДанных = Элементы.Добавить(ПриемПищи,Тип("ТаблицаФормы"));
		ТаблицаДанных.ПутьКДанным = "Таблица"+ПриемПищи;
		ТаблицаДанных.Отображение = ОтображениеТаблицы.Список;
		ТаблицаДанных.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
		
		Для Каждого Колонка Из ТЗ.Колонки Цикл
			
			НовыйЭлемент = Элементы.Добавить(Колонка.Имя, Тип("ПолеФормы"),ТаблицаДанных);
			НовыйЭлемент.Заголовок = СтрЗаменить(Колонка.Имя,ПриемПищи,"");
			НовыйЭлемент.Заголовок = ?(НовыйЭлемент.Заголовок = "ПищеваяЦенность","Пищевая ценность",НовыйЭлемент.Заголовок);
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
			НовыйЭлемент.ПутьКДанным = "Таблица"+ПриемПищи+"." + Колонка.Имя; 
			НовыйЭлемент.Ширина = 10; 
				
		КонецЦикла;
		
		ЗначениеВРеквизитФормы(ТЗ, "Таблица"+ПриемПищи);
		
		НазванияТаблиц.Добавить("Таблица"+ПриемПищи);

	КонецЦикла;	   
	
	ОбщиеФункцииНаСервере.ПересчетИтоговыхРезультатов(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПриемПищи(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаЗначения", ЭтотОбъект);	
    ПоказатьВводЗначения(Оповещение,"","Введите название приема пищи",Тип("Строка"));
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаЗначения(Результат, Параметры) Экспорт	
 
    Если Результат = Неопределено Тогда
        Возврат;
	КонецЕсли;
	
	ТекЗначение = НазванияТаблиц.НайтиПоЗначению("Таблица"+Результат);
	Если ТекЗначение <> Неопределено Тогда
		Сообщить("Прием пищи с таким названием уже существует");
		Возврат;
	КонецЕсли;
	
	ДобавитьТаблицуПриемаПищи(Результат);
	 
КонецПроцедуры

&НаСервере
Процедура ДобавитьТаблицуПриемаПищи(ПриемПищи)
	
	МассивТипаВыбора = Новый Массив;
	МассивТипаВыбора.Добавить(Тип("ТаблицаЗначений"));
	ОписаниеТипаВыбора = Новый ОписаниеТипов(МассивТипаВыбора);
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить(Новый РеквизитФормы("Таблица"+ПриемПищи, ОписаниеТипаВыбора, "", ПриемПищи));
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 0
	               |	ПитаниеПродукты.Продукт КАК Продукт"+ПриемПищи+",
	               |	ПитаниеПродукты.Порция КАК Порция"+ПриемПищи+",
				   |	ПитаниеПродукты.Продукт.Белки * ПитаниеПродукты.Порция / ЕСТЬNULL(ПитаниеПродукты.Продукт.ЕдиницаИзмерения.Кратность,1) КАК Белки"+ПриемПищи+",
	               |	ПитаниеПродукты.Продукт.Жиры * ПитаниеПродукты.Порция / ЕСТЬNULL(ПитаниеПродукты.Продукт.ЕдиницаИзмерения.Кратность,1) КАК Жиры"+ПриемПищи+",
	               |	ПитаниеПродукты.Продукт.Углеводы * ПитаниеПродукты.Порция / ЕСТЬNULL(ПитаниеПродукты.Продукт.ЕдиницаИзмерения.Кратность,1) КАК Углеводы"+ПриемПищи+",
	               |	ПитаниеПродукты.Продукт.ПищеваяЦенность * ПитаниеПродукты.Порция / ЕСТЬNULL(ПитаниеПродукты.Продукт.ЕдиницаИзмерения.Кратность,1) КАК ПищеваяЦенность"+ПриемПищи+"
	               |ИЗ
	               |	Документ.Питание.Продукты КАК ПитаниеПродукты
				   |ГДЕ
				   |	ПитаниеПродукты.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
				   |";
		
	Запрос.УстановитьПараметр("ДатаНачала",НачалоДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("ДатаОкончания",КонецДня(ТекущаяДата()));
	
	ТЗ = Запрос.Выполнить().Выгрузить();                           
	
	Для Каждого Колонка Из ТЗ.Колонки Цикл
		
		МассивРеквизитов.Добавить(Новый РеквизитФормы(Колонка.Имя,Колонка.ТипЗначения,"Таблица"+ПриемПищи));
		
	КонецЦикла;
	
	ИзменитьРеквизиты(МассивРеквизитов);
	
	ТаблицаДанных = Элементы.Добавить(ПриемПищи,Тип("ТаблицаФормы"));
	ТаблицаДанных.ПутьКДанным = "Таблица"+ПриемПищи;
	ТаблицаДанных.Отображение = ОтображениеТаблицы.Список;
	ТаблицаДанных.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	
	Для Каждого Колонка Из ТЗ.Колонки Цикл
		
		НовыйЭлемент = Элементы.Добавить(Колонка.Имя, Тип("ПолеФормы"),ТаблицаДанных);
		НовыйЭлемент.Заголовок = СтрЗаменить(Колонка.Имя,ПриемПищи,"");
		НовыйЭлемент.Заголовок = ?(НовыйЭлемент.Заголовок = "ПищеваяЦенность","Пищевая ценность",НовыйЭлемент.Заголовок);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным = "Таблица"+ПриемПищи+"." + Колонка.Имя; 
		НовыйЭлемент.Ширина = 10; 
			
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТЗ, "Таблица"+ПриемПищи);
	
	НазванияТаблиц.Добавить("Таблица"+ПриемПищи);
  	
	ОбщиеФункцииНаСервере.ПересчетИтоговыхРезультатов(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура УдалитьПриемПищи(Команда)
	
	Если НЕ НазванияТаблиц.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("НазванияТаблиц",НазванияТаблиц);
	ОткрытьФорму("Документ.Питание.Форма.ФормаВыбораПриемаПищи", ПараметрыФормы, ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
			
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "УдалениеПриемаПищи" Тогда
		УдалитьПриемПищиНаСервере(Параметр);	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьПриемПищиНаСервере(НазваниеТаблицы)
	
	УдаляемыеРеквизитыТЗ = Новый Массив;
	УдаляемыеРеквизитыТЗ.Добавить("Таблица"+НазваниеТаблицы+".Продукт"+НазваниеТаблицы); 
	УдаляемыеРеквизитыТЗ.Добавить("Таблица"+НазваниеТаблицы+".Порция"+НазваниеТаблицы); 
	УдаляемыеРеквизитыТЗ.Добавить("Таблица"+НазваниеТаблицы+".Белки"+НазваниеТаблицы); 
	УдаляемыеРеквизитыТЗ.Добавить("Таблица"+НазваниеТаблицы+".Жиры"+НазваниеТаблицы); 
	УдаляемыеРеквизитыТЗ.Добавить("Таблица"+НазваниеТаблицы+".Углеводы"+НазваниеТаблицы); 
	УдаляемыеРеквизитыТЗ.Добавить("Таблица"+НазваниеТаблицы+".ПищеваяЦенность"+НазваниеТаблицы); 		
	УдаляемыеРеквизитыТЗ.Добавить("Таблица"+НазваниеТаблицы);
	
	Элементы.Удалить(Элементы["Продукт"+НазваниеТаблицы]);
	Элементы.Удалить(Элементы["Порция"+НазваниеТаблицы]);
	Элементы.Удалить(Элементы["Белки"+НазваниеТаблицы]);
	Элементы.Удалить(Элементы["Жиры"+НазваниеТаблицы]);
	Элементы.Удалить(Элементы["Углеводы"+НазваниеТаблицы]);
	Элементы.Удалить(Элементы["ПищеваяЦенность"+НазваниеТаблицы]);
	Элементы.Удалить(Элементы[НазваниеТаблицы]);
	
	ЭтаФорма.ИзменитьРеквизиты(, УдаляемыеРеквизитыТЗ);
	
	ТекЗначение = НазванияТаблиц.НайтиПоЗначению("Таблица"+НазваниеТаблицы);
	
	Если ТекЗначение <> Неопределено Тогда
		НазванияТаблиц.Удалить(ТекЗначение);
	КонецЕсли;
	
	ОбщиеФункцииНаСервере.ПересчетИтоговыхРезультатов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРезультаты(Команда)
	
	ОбновитьРезультатыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьРезультатыНаСервере()
	
	ОбщиеФункцииНаСервере.ПересчетКаллорийности(ЭтаФорма);
	ОбщиеФункцииНаСервере.ПересчетИтоговыхРезультатов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьРезультат(Команда)
	СохранитьРезультатНаСервере();
КонецПроцедуры

&НаСервере
Процедура СохранитьРезультатНаСервере()
	
	Попытка
	
		Объект.Продукты.Очистить();
		
		Для Каждого Таблица Из ЭтаФорма.НазванияТаблиц Цикл
			
			НазваниеТаблицы = СтрЗаменить(Таблица.Значение,"Таблица","");
			
			Для Каждого ТекСтрока Из ЭтаФорма[Таблица.Значение] Цикл
				
				НовСтр = Объект.Продукты.Добавить();
				
				ПриемПищи = Справочники.ПриемыПищи.НайтиПоНаименованию(НазваниеТаблицы,Истина);
				Если ПриемПищи.Пустая() Тогда
					НовПриемПищи = Справочники.ПриемыПищи.СоздатьЭлемент();
					НовПриемПищи.Наименование = НазваниеТаблицы;
					НовПриемПищи.Записать();
					ПриемПищи = НовПриемПищи.Ссылка;
				КонецЕсли;
									
				НовСтр.ПриемПищи = ПриемПищи;
				НовСтр.Продукт = ТекСтрока["Продукт"+НазваниеТаблицы];
				НовСтр.Порция = ТекСтрока["Порция"+НазваниеТаблицы];
				
			КонецЦикла;
			
		КонецЦикла;
		
		ПараметрыЗаписи = Новый Структура("РежимЗаписи",РежимЗаписиДокумента.Проведение);
		ЭтаФорма.Записать(ПараметрыЗаписи);
		
	Исключение
		Сообщить("Не удалось сохранить день");
		Возврат;
	КонецПопытки;
	
КонецПроцедуры
