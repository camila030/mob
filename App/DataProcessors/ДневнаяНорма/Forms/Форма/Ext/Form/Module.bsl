
&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьДиаграмму();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДиаграмму()
	
	Диаграмма.Обновление = Ложь;
	
	ДанныеДиаграммы = ПолучитьДанные(ДатаПарам);
	
	//Белки
	Диаграмма.УстановитьЗначение(Диаграмма.Точки[0],Диаграмма.Серии[0],ДанныеДиаграммы.БелкиТекущие);
	Диаграмма.УстановитьЗначение(Диаграмма.Точки[0],Диаграмма.Серии[1],ДанныеДиаграммы.БелкиНорма);
	//Жиры
	Диаграмма.УстановитьЗначение(Диаграмма.Точки[1],Диаграмма.Серии[0],ДанныеДиаграммы.ЖирыТекущие);
	Диаграмма.УстановитьЗначение(Диаграмма.Точки[1],Диаграмма.Серии[1],ДанныеДиаграммы.ЖирыНорма);
	//Углеводы
	Диаграмма.УстановитьЗначение(Диаграмма.Точки[2],Диаграмма.Серии[0],ДанныеДиаграммы.УглеводыТекущие);
	Диаграмма.УстановитьЗначение(Диаграмма.Точки[2],Диаграмма.Серии[1],ДанныеДиаграммы.УглеводыНорма);
	//Пищевая ценность
	Диаграмма.УстановитьЗначение(Диаграмма.Точки[3],Диаграмма.Серии[0],ДанныеДиаграммы.ПищеваяЦенностьТекущие);
	Диаграмма.УстановитьЗначение(Диаграмма.Точки[3],Диаграмма.Серии[1],ДанныеДиаграммы.ПищеваяЦенностьНорма);
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанные(Дата)
	
	СтруктураОтвета = Новый Структура("БелкиТекущие,ЖирыТекущие,УглеводыТекущие,ПищеваяЦенностьТекущие,БелкиНорма,ЖирыНорма,УглеводыНорма,ПищеваяЦенностьНорма");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала",НачалоДня(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания",КонецДня(Дата));
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	СУММА(ПитаниеПродукты.Продукт.Белки * ПитаниеПродукты.Порция / ЕСТЬNULL(ПитаниеПродукты.Продукт.ЕдиницаИзмерения.Кратность, 1)) КАК Белки,
	               |	СУММА(ПитаниеПродукты.Продукт.Жиры * ПитаниеПродукты.Порция / ЕСТЬNULL(ПитаниеПродукты.Продукт.ЕдиницаИзмерения.Кратность, 1)) КАК Жиры,
	               |	СУММА(ПитаниеПродукты.Продукт.Углеводы * ПитаниеПродукты.Порция / ЕСТЬNULL(ПитаниеПродукты.Продукт.ЕдиницаИзмерения.Кратность, 1)) КАК Углеводы,
	               |	СУММА(ПитаниеПродукты.Продукт.ПищеваяЦенность * ПитаниеПродукты.Порция / ЕСТЬNULL(ПитаниеПродукты.Продукт.ЕдиницаИзмерения.Кратность, 1)) КАК ПищеваяЦенность
	               |ИЗ
	               |	Документ.Питание.Продукты КАК ПитаниеПродукты
	               |ГДЕ
	               |	ПитаниеПродукты.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания";
	
	РЗ = Запрос.Выполнить().Выгрузить();
	
	Если РЗ.Количество() = 0 Тогда
		СтруктураОтвета.БелкиТекущие = 0;
		СтруктураОтвета.ЖирыТекущие = 0;
		СтруктураОтвета.УглеводыТекущие = 0;
		СтруктураОтвета.ПищеваяЦенностьТекущие = 0;
	Иначе
		ТекСтрока = РЗ.Получить(0);
		СтруктураОтвета.БелкиТекущие = ТекСтрока.Белки;
		СтруктураОтвета.ЖирыТекущие = ТекСтрока.Жиры;
		СтруктураОтвета.УглеводыТекущие = ТекСтрока.Углеводы;
		СтруктураОтвета.ПищеваяЦенностьТекущие = ТекСтрока.ПищеваяЦенность;
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	НормыКилокалорий.НормаКаллорий КАК НормаКаллорий,
	               |	НормыКилокалорий.Белки КАК Белки,
	               |	НормыКилокалорий.Жиры КАК Жиры,
	               |	НормыКилокалорий.Углеводы КАК Углеводы
	               |ИЗ
	               |	РегистрСведений.ИзмеренияЧеловека.СрезПоследних(&Период, ) КАК ИзмеренияЧеловекаСрезПоследних
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НормыКилокалорий КАК НормыКилокалорий
	               |		ПО ИзмеренияЧеловекаСрезПоследних.Пользователь.Возраст >= НормыКилокалорий.ВозрастОт
	               |			И ИзмеренияЧеловекаСрезПоследних.Пользователь.Возраст <= НормыКилокалорий.ВозрастДо
	               |			И ИзмеренияЧеловекаСрезПоследних.Пользователь.Пол = НормыКилокалорий.Пол
	               |			И ИзмеренияЧеловекаСрезПоследних.Пользователь.УровеньАктивности = НормыКилокалорий.УровеньАктивности";
	
	Запрос.УстановитьПараметр("Период",Дата);
	
	РЗ = Запрос.Выполнить().Выгрузить();
	
	Если РЗ.Количество() = 0 Тогда
		СтруктураОтвета.БелкиНорма = 0;
		СтруктураОтвета.ЖирыНорма = 0;
		СтруктураОтвета.УглеводыНорма = 0;
		СтруктураОтвета.ПищеваяЦенностьНорма = 0;
	Иначе
		ТекСтрока = РЗ.Получить(0);
		СтруктураОтвета.БелкиНорма = ТекСтрока.Белки;
		СтруктураОтвета.ЖирыНорма = ТекСтрока.Жиры;
		СтруктураОтвета.УглеводыНорма = ТекСтрока.Углеводы;
		СтруктураОтвета.ПищеваяЦенностьНорма = ТекСтрока.НормаКаллорий;
	КонецЕсли;
				   
	Возврат СтруктураОтвета;	
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ДатаПарам = ТекущаяДата();
	
	ОбновитьДиаграмму();
	
КонецПроцедуры
