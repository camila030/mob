
&НаКлиенте
Процедура СделатьФото(Команда)
	
	#Если МобильноеПриложениеКлиент Тогда
		ДанныеМультимедиа = СредстваМультимедиа.СделатьФотоснимок();
		
		Если ДанныеМультимедиа <> Неопределено Тогда
			СсылкаНаКартинку = ПоместитьВоВременноеХранилище(ДанныеМультимедиа.ПолучитьДвоичныеДанные(), УникальныйИдентификатор);
		КонецЕсли;
	#КонецЕсли

КонецПроцедуры

&НаСервере
Процедура СоздатьНовыйФайлСервер(Данные, Расширение, Тип)
	
	СсылкаНаКартинку = ПоместитьВоВременноеХранилище(Данные, УникальныйИдентификатор);
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(СсылкаНаКартинку) Тогда
		ТекущийОбъект.ДанныеФото = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(СсылкаНаКартинку),Новый СжатиеДанных(9));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СсылкаНаКартинку = ПолучитьНавигационнуюСсылку(Объект.Ссылка,"ДанныеФото");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзГалереи(Команда)
	
	#Если МобильноеПриложениеКлиент Тогда
	
		НовВз = Новый ЗапускПриложенияМобильногоУстройства("android.intent.action.GET_CONTENT");
	    НовВз.Тип = "image/*";
	    НовВз.ДополнительныеДанные.Добавить("android.intent.extra.LOCAL_ONLY",Истина);
	    РезультатРаботы = НовВз.Запустить(Истина);
	    
	    Если НЕ РезультатРаботы Тогда
	        Сообщить("Файл не выбран!");
	        Возврат;
		Иначе
			Если СтрНайти(НовВз.Данные,"content") > 0 Тогда
				Сообщить("Ваше устройство не поддерживает сохранение из Галереи");
				Возврат;
			КонецЕсли;
	        ОткрытьГалереюНаСервере(НовВз.Данные);
		КонецЕсли;
		
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ОткрытьГалереюНаСервере(ИмяФайла)
	
	ДвоичДанные = Новый ДвоичныеДанные(ИмяФайла);
    СсылкаНаКартинку = ПоместитьВоВременноеХранилище(ДвоичДанные, УникальныйИдентификатор);
	
КонецПроцедуры